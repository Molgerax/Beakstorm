#pragma kernel InitializeKernel
#pragma kernel UpdateKernel
#pragma kernel RenderKernel
#pragma kernel KillKernel
#pragma kernel EmitKernel
#pragma kernel EmissionCountKernel

#pragma multi_compile _ PARTICLE_PONG_BUFFER

// Must contain a Particle definition, which must at least contain 'bool alive'
#define PARTICLE_STRUCT Particle
struct PARTICLE_STRUCT
{
    bool alive;
    float3 position;
    float3 oldPosition;
    float3 color;
    float3 normal;
    float3 tangent;

    float age;
    float life;
};

#include "../ParticleTemplate.hlsl"
#include "../ParticleUtility.hlsl"
#include "../ParticleRenderUtility.hlsl"


float _Gravity;
float _Y;


// Standard kernels to initialize and kill particles.
INITIALIZE
KILL


UPDATE
{
    const uint index = id.x;

    PARTICLE_STRUCT p = _ParticleBuffer[index];

    if(p.alive)
    {
        float3 velocity = (p.position - p.oldPosition) / _DeltaTime;

        p.oldPosition = p.position;
        p.position += velocity * _DeltaTime;

        p.position.y += 0.5 * _Gravity * _DeltaTime * _DeltaTime;

        if(p.position.y < -5)
        {
            p.alive = false;
            _DeadIndexBuffer.Append(index);
        }
        Collision_PlaneY(p.position, p.oldPosition, _Y, 0.1, 0.5);
        
    }

    SetParticle(index, p);
}

RENDER
{
    const uint index = id.x;

    PARTICLE_STRUCT p = _ParticleBuffer[index];
    
    if(!p.alive) return;

    RenderVertex startV = (RenderVertex)0;
    startV.positionWS = p.position;
    startV.normalWS = p.normal;
    startV.tangentWS = float4(p.tangent, 1);
    startV.color = float4(p.color, 1);
    startV.uv = 0;

    RenderVertex endV = startV;
    endV.positionWS += endV.normalWS * 1;
    endV.uv = float2(1, 0);
        
    Add_Tube4(startV, endV, 1, 1);
}

SET_EMISSION_TO_DEADCOUNT

EMIT
{
    const uint index = _AliveIndexBuffer.Consume();
    
    PARTICLE_STRUCT p = (PARTICLE_STRUCT)0;

    float randomSeed = Random( (float)(index * 2 + id.x + _time + _DeltaTime + dot(_WorldPosition, _WorldPositionOld + 3) * 13.4));
    
    p.alive = true;
    p.position = _WorldPosition + GetInterpolatedLocation(_WorldPosition, _WorldPositionOld, randomSeed);
    p.oldPosition = p.position - GetInheritedVelocity(_WorldPosition, _WorldPositionOld, 1, _DeltaTime, randomSeed);
    p.normal = mul(_WorldMatrix, float4(0,1,0,0)).xyz;
    p.tangent = mul(_WorldMatrix, float4(0,0,1,0)).xyz;
    p.color = float3(0.5,0.5,1);

    _ParticleBuffer[index] = p;
}

