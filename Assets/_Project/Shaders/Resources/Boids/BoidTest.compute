#pragma kernel Update
#pragma kernel Init

#pragma kernel UpdateSpatialHash
#pragma kernel SpawnPheromone

#include "../SpatialHashing/RandomFunctions.hlsl"

#include "BoidParameters.hlsl"
#include "BoidStateMachine.hlsl"

#include "../Collisions/SdfCollisions.hlsl"
#include "../Pheromones/PheromoneMath.hlsl"

#define THREAD_BLOCK_SIZE 256



float UvOutOfBounds(float2 uv)
{
    float2 q = step(1, uv) + (step(uv, 0));
    return 1-step(dot(q, q), 0);
}


float3 safeNormalize(float3 value)
{
    if (dot(value, value) == 0)
        return float3(0, 1, 0);
    return normalize(value);

}

float3 limitVector(float3 v, float maxLength)
{
    if (dot(v, v) == 0)
        return 0;
    float len = length(v);
    return (v / len) * min(len, maxLength);
}

float3 clampVector(float3 v, float min, float max)
{
    if (dot(v, v) == 0)
        return 0;
    float len = length(v);
    return (v / len) * clamp(len, min, max);
}

float3 calcSteerForce(float3 steer, float3 currentVelocity, float maxSpeed, float maxForce)
{
    float3 desiredVelocity = safeNormalize(steer) * maxSpeed;
    return limitVector(desiredVelocity - currentVelocity, maxForce);
}


float3 forceFromBounds(float3 pos)
{
    float dist = step(0, sdfBox(pos, 0, _SimulationSpace));
    float3 p = ClosestPointOnBox(0, _SimulationSpace * 0.5, pos);
    float3 v = p - pos;
    float3 force = clampVector(v, 10, GetMaxForce(0));
    return force * dist;
}

// Collision
void collideBounds(inout float3 pos, inout float3 oldPos)
{
    float dist = sdfBox(pos, 0, _SimulationSpace * 0.5);
    
    if (dist < 0)
    {
        return;
    }
    float3 p = ClosestPointOnBox(0, _SimulationSpace * 0.5, pos);
    float3 diff = p - pos;
    float3 normal = normalize(diff);

    float3 flattenVelocity = dot(pos - oldPos - normal * abs(dist), -normal) * normal;
    float3 mostBounceVelocity = dot(pos - oldPos, normal) * normal;
           
    oldPos = lerp(oldPos - flattenVelocity, oldPos + mostBounceVelocity, clamp(0, 0, 1));
    pos += normal * dist;
}

void collideSdf(inout float3 pos, inout float3 oldPos, SdfQueryInfo sdf, float bounce)
{
    float dist = sdf.dist;
    float3 normal = sdf.normal;
    if (dist > 0)
    {
        return;
    }
    float3 vel = pos - oldPos;
    float3 flattenVelocity = -dot(vel + normal * -dist, normal) * normal;
    float3 mostBounceVelocity = -dot(vel, normal) * normal;
           
    oldPos = lerp(oldPos - flattenVelocity, oldPos - mostBounceVelocity, saturate(bounce));
    pos -= normal * dist;
}

float3 centerForce(float3 pos, uint index)
{
    float radius = 100;
    float len = length(pos);
    float3 force = -safeNormalize(pos) * max(0, len - radius);
    return limitVector(force, 100);
}

float3 whistleForce(float3 pos)
{
    float radius = 100;
    float3 diff = (_WhistleSource.xyz - pos);
    float3 force = safeNormalize(diff) * max(0, length(diff) - radius) * _WhistleSource.w;
    return limitVector(force, 1000);
}

float3 sdfForce(SdfQueryInfo info)
{
    float3 f = info.normal;
    float strength = -min(0, info.dist - 3);
    return f * strength;
}

float3 FlockingForce(uint index)
{
    float4 boidRadius = GetBoidRadius(index);
    float maxSpeed = GetMaxSpeed(index);
    float maxForce = GetMaxForce(index);
    
    float3 positionA = _PositionBuffer[index];
    float3 oldPositionA = _OldPositionBuffer[index];
    float3 velocityA = (positionA - oldPositionA) / _DeltaTime;

    
    int4 numFlockMates = 0;
    float3 alignmentSum = 0;
    float3 cohesionSum = 0;
    float3 separationSum = 0;
    float detectionSum = 0;
    float highestDetection = 0;    
    float3 flockCenter;

    // Neighborhood Search using Spatial Hash Grids
    float largestRadius = max(max(boidRadius.x, boidRadius.y), max(boidRadius.z, boidRadius.w));
    int3 originCell = GetCell3D(positionA, _HashCellSize);


    
    for (int i = 0; i < 27; i++)
    {
        uint hashCell = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hashCell, _TotalCount);
        uint currIndex = _BoidSpatialOffsets[key];
        
        
        while (currIndex < _TotalCount)
        {
            uint3 indexData = _BoidSpatialIndices[currIndex];
            currIndex++;
            // Exit if no longer looking at correct bin
            if (indexData[2] != key)
                break;
			// Skip if hash does not match
            if (indexData[1] != hashCell)
                continue;
            
            uint neighborIndex = indexData[0];
        
            if(neighborIndex == index)
                continue;
            
            float3 positionB = _PositionBuffer[neighborIndex];
            float3 oldPositionB = _OldPositionBuffer[neighborIndex];
            float3 velocityB = (positionB - oldPositionB) / _DeltaTime;
            float4 dataB = _DataBuffer[neighborIndex];
            
            float3 offset = positionB - positionA;
            
            if (dot(offset, offset) > largestRadius * largestRadius)
                continue;

            if (dot(velocityA, velocityB) < 0) continue;
            
            float distance = length(offset);
            
            
            // separation
            if (distance > 0 && distance < boidRadius.x)
            {
                numFlockMates.x++;
                separationSum -= offset / (distance * distance);
            }
            
            // alignment
            if(distance > 0 && distance < boidRadius.y)
            {
                numFlockMates.y++;
                alignmentSum += velocityB;
            }
            
            // cohesion
            if (distance > 0 && distance < boidRadius.z)
            {
                numFlockMates.z++;
                cohesionSum += offset;
                flockCenter += positionB;
            }

            // detection
            if (distance > 0 && distance < boidRadius.w)
            {
                numFlockMates.w++;
                detectionSum += dataB.w / (1);
                highestDetection = max(detectionSum, highestDetection);
            }
        }
    }
    
    float3 alignmentForce = 0;
    float3 cohesionForce = 0;
    float3 separationForce = 0;

    
    if (numFlockMates.x != 0)
    {
        separationForce = calcSteerForce(separationSum, velocityA, maxSpeed, maxForce);
    }
    
    if(numFlockMates.y != 0)
    {
        alignmentForce = calcSteerForce(alignmentSum, velocityA, maxSpeed, maxForce);
    }
    
    if (numFlockMates.z != 0)
    {
        flockCenter /= numFlockMates.z;
        float3 offsetToFlockCenter = flockCenter - positionA;
        cohesionForce = calcSteerForce(offsetToFlockCenter, velocityA, maxSpeed, maxForce);
    }

    if (numFlockMates.w != 0)
    {
        detectionSum /= numFlockMates.w;
        //detectionSum = min(detectionSum, highestDetection);
    }
    

    float4 factors = GetBoidSettings(index);
    
    float3 acceleration = 0;
    acceleration += separationForce * factors.x;
    acceleration += alignmentForce * factors.y;
    acceleration += cohesionForce * factors.z;

    float4 data = _DataBuffer[index];
    data.y = detectionSum;
    _DataBuffer[index] = data;
    
    return acceleration;
}


float4 PheromoneDensity(float3 pos)
{
    int numNeighbors = 0;
    float3 densityGradient = 0;
    float density = 0;
    
    // Neighborhood Search using Spatial Hash Grids
    int3 originCell = GetCell3D(pos, _PheromoneHashCellSize);
    
    for (int i = 0; i < 27; i++)
    {
        uint hashCell = HashCell3D(originCell + offsets3D[i]);
        uint key = KeyFromHash(hashCell, _PheromoneTotalCount);
        uint currIndex = _PheromoneSpatialOffsets[key];
        
        
        while (currIndex < _PheromoneTotalCount)
        {
            uint3 indexData = _PheromoneSpatialIndices[currIndex];
            currIndex++;
            // Exit if no longer looking at correct bin
            if (indexData[2] != key)
                break;
            // Skip if hash does not match
            if (indexData[1] != hashCell)
                continue;
            
            uint neighborIndex = indexData[0];
            float life = _PheromoneAliveBuffer[neighborIndex];
            if (life <= 0)
                continue;
            
            
            float3 positionB = _PheromonePositionBuffer[neighborIndex];
            float4 dataB = _PheromoneDataBuffer[neighborIndex];
            float3 offset = positionB - pos;
            float distSquared = dot(offset, offset);
            
            if (distSquared > _PheromoneHashCellSize * _PheromoneHashCellSize)
                continue;
            numNeighbors++;

            float d = GetDensityFromParticle(pos, positionB, _PheromoneHashCellSize);
            float3 g = GetDensityDerivativeFromParticle(pos, positionB, _PheromoneHashCellSize, 1);
            
            density += d;
            densityGradient += g * (dataB.w * dataB.w) / dataB.x;
        }
    }
    
    return float4(densityGradient, density);
}

[numthreads(THREAD_BLOCK_SIZE,1,1)]
void Update (uint3 id : SV_DispatchThreadID)
{
    const uint index = id.x;
    
    float3 currentPosition = _PositionBuffer[index];
    float3 oldPosition = _OldPositionBuffer[index];
    float3 normal = _NormalBuffer[index];
    
    float3 velocity = (currentPosition - oldPosition) / _DeltaTime;
    oldPosition = currentPosition;
    
    float3 tangent = safeNormalize(cross(normal, velocity));
    
    SdfQueryInfo sdfInfo = GetClosestDistance(currentPosition, 0);
    
    float3 acceleration = 0;
    acceleration += FlockingForce(index);
    //acceleration += forceFromBounds(currentPosition);
    //acceleration += centerForce(currentPosition, index);
    //acceleration += sdfForce(sdfInfo);
    acceleration += whistleForce(currentPosition);


    float4 factors = GetBoidSettings(index);
    float maxSpeed = GetMaxSpeed(index);
    float maxForce = GetMaxForce(index);
    float4 pheromoneSample = PheromoneDensity(currentPosition + velocity * _DeltaTime * 1) * 100;
    float3 pheromoneForce = calcSteerForce(pheromoneSample.xyz, velocity, maxSpeed, maxForce);

    acceleration += pheromoneForce * factors.w * saturate(pheromoneSample.w);
    
    velocity += acceleration * _DeltaTime;
    velocity = clampVector(velocity, GetMinSpeed(index), GetMaxSpeed(index));

    currentPosition += velocity * _DeltaTime;
    //collideBounds(currentPosition, oldPosition);
    collideSdf(currentPosition, oldPosition, sdfInfo, 0.1);
    
    //currentPosition.y = _WorldPos.y;
    
    _PositionBuffer[index] = currentPosition;
    _OldPositionBuffer[index] = oldPosition;

    normal = safeNormalize(cross(velocity, tangent));

    float3 v = normalize(currentPosition - oldPosition);
    //v = float3(0, 1, 0);
    //if (dot(sdfInfo.normal, sdfInfo.normal) > 0)
    //    v = sdfInfo.normal;

    //if (dot(pheromoneSample.xyz, pheromoneSample.xyz) > 0)
    //    v = pheromoneSample.xyz;
    
    //v = float3(0, 0, 1);
    //normal = float3(0, 1, 0);
    //normal = safeNormalize(cross(v, float3(1, 0, 0)));
    _VelocityBuffer[index] = v;
    _NormalBuffer[index] = normal;


    float4 data = _DataBuffer[index];
    //data.w = max(data.w, data.y);
    data.w = saturate(data.w + pheromoneSample.w - _DeltaTime * 1);
    //data.w = saturate(data.w + pheromoneSample.w);
    
    //data.w = frac(sdfInfo.dist * 0.2) * (sdfInfo.matIndex / 4.0);
    
    _DataBuffer[index] = data;
}

[numthreads(THREAD_BLOCK_SIZE, 1, 1)]
void Init(uint3 id : SV_DispatchThreadID)
{
    const uint index = id.x;
    
    int sideLength = sqrt(_TotalCount);

    int2 newId = int2(index / sideLength, index % sideLength);
    
    float stagger = ((float)index / _TotalCount) * 2;// - 1;
    float3 offset = - _WorldMatrix._m20_m21_m22 * stagger * _TotalCount;

    //offset = _WorldMatrix._m00_m01_m02 * stagger * 32;
    offset = (RandomVector01(index) - 0.5) * _SimulationSpace;

    //offset = 0;
    //offset.xz = ((float2)newId / sideLength - 0.5) * _SimulationSpace.xz; 
    
    float3 currentPosition = offset + _WorldPos;
    float3 currentNormal = _WorldMatrix._m10_m11_m12;
    float3 currentVelocity = _WorldMatrix._m20_m21_m22 * GetMaxSpeed(index);

    currentVelocity = RandomVector(index) * 0.001;
    float3 oldPosition = currentPosition - currentVelocity * _DeltaTime;
    
    _PositionBuffer[index] = currentPosition;
    _OldPositionBuffer[index] = oldPosition;
    _VelocityBuffer[index] = currentVelocity;
    _NormalBuffer[index] = currentNormal;
    _DataBuffer[index] = 0;
}

[numthreads(THREAD_BLOCK_SIZE, 1, 1)]
void UpdateSpatialHash(uint3 id : SV_DispatchThreadID)
{
    const uint index = id.x;
    
    if (index >= _TotalCount)
        return;
    
    float3 position = _PositionBuffer[index];

    SetSpatialHash_Boid(index, position, _TotalCount, _HashCellSize);
}

ConsumeStructuredBuffer<uint> _PheromoneAliveIndexBuffer;
RWStructuredBuffer<float> _PheromoneAliveBufferWrite;
RWStructuredBuffer<float3> _PheromonePositionBufferWrite;
RWStructuredBuffer<float3> _PheromoneOldPositionBufferWrite;
RWStructuredBuffer<float4> _PheromoneDataBufferWrite;

[numthreads(THREAD_BLOCK_SIZE,1,1)]
void SpawnPheromone (uint3 id : SV_DispatchThreadID)
{
    return;
    const uint index = id.x;
    const float spawnRate = 10;
    float lifeTime = 1;

    bool emit = false;
    
    float3 pos = _PositionBuffer[index];
    float3 oldPos = _OldPositionBuffer[index];
    
    float4 factors = GetBoidSettings(index);
    float maxSpeed = GetMaxSpeed(index);
    float maxForce = GetMaxForce(index);
    float4 data = _DataBuffer[index];
    
    float pheromoneExposure = data.w;
    float emissionTimeStep = _DeltaTime * pheromoneExposure * spawnRate;

    lifeTime = pheromoneExposure * 1;
    
    // Emission Time
    data.z *= 0.75;
    data.z += emissionTimeStep;
    if (data.z >= 1)
        emit = true;

    data.z = frac(data.z);

    _DataBuffer[index] = data;

    if (!emit)
        return;

    if (DeadParticleCount() < 1024)
        return;
    
    const uint pheromoneIndex = _PheromoneAliveIndexBuffer.Consume();
    float randomOffset = Random(pheromoneIndex) * 1.0;

    pos = oldPos;
    
    _PheromoneAliveBufferWrite[pheromoneIndex] = lifeTime;
    
    _PheromonePositionBufferWrite[pheromoneIndex] = pos;
    _PheromoneOldPositionBufferWrite[pheromoneIndex] = oldPos;
    _PheromoneDataBufferWrite[pheromoneIndex] = lifeTime;
}